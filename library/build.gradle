apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    PUBLISH_GROUP_ID = "com.github.kxfeng"
    PUBLISH_ARTIFACT_ID = 'logs'
    PUBLISH_VERSION = '1.1.1'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.1.1"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        kotlinOptions.freeCompilerArgs += ['-module-name', "$PUBLISH_GROUP_ID.$PUBLISH_ARTIFACT_ID"]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation 'junit:junit:4.12'
}

bintray {
    user = findProperty('BINTRAY_USER')
    key = findProperty('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = "$PUBLISH_ARTIFACT_ID"
        version {
            name = "$PUBLISH_VERSION"
            desc = "$PUBLISH_VERSION release"
            released  = new Date()
            vcsTag = "$PUBLISH_VERSION"
        }
    }
    publications = ['MyPublication']
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.android
            groupId = "$PUBLISH_GROUP_ID"
            artifactId = "$PUBLISH_ARTIFACT_ID"
            version = "$PUBLISH_VERSION"
            artifact sourceJar
        }
    }
}